# -*- mode: Snakemake -*-
# Sequence Alignment: BLAT

# Align sequences to designated human genome
rule align:
  input:
    seq = PROC_DIR + "/analysis_data/{sample}.{read}.consol.fasta",
    genome = config["Ref_Genome_Path"]
  output:
    PROC_DIR + "/analysis_data/{sample}.{read}.psl"
  params:
    config["BLATparams"]
  log:
    PROC_DIR + "/logs/{sample}.{read}.blat.log"
  resources:
    mem_mb = lambda wildcards, attempt: attempt * config["alignMB"]
  shell:
    """
    if [[ $(cat {input.seq} | wc -l) -eq 0 ]]
        then
            touch {output}
            echo 'Empty input sequences for {input.seq}.' > {log} 2>&1
        else
            blat {input.genome} {input.seq} {output} \
                {params} > {log} 2>&1
    fi
    """

# Compress the alignment files
rule compress_align:
  input: PROC_DIR + "/analysis_data/{sample}.{read}.psl"
  output: PROC_DIR + "/analysis_data/{sample}.{read}.psl.gz"
  shell: "gzip {input}"

# Generate 2bit blat alignment file
rule generate_2bit:
  output: temp(config["Ref_Genome_Path"])
  params: config["Ref_Genome"]
  shell:
    "Rscript {CODE_DIR}/generate_2bit_genome.R {params} {output}"

